

/*
 * Executes all KPI Hyperlambda snippets and returns to caller.
 */
.description:Executes all KPI Hyperlambda snippets and returns to caller

// Opening up our database connection.
data.connect:[generic|aista-crm]
   database-type:sqlite
   
   data.read
      table:kpi
      columns
         hyperlambda
         type
         name
      limit:-1
   for-each:x:@data.read/*
      .return
      hyper2lambda:x:@.dp/#/*/hyperlambda
      invoke:x:@hyper2lambda
      unwrap:x:+/*/*/*
      add:x:@.return
         .
            .
               name:x:@.dp/#/*/name
               type:x:@.dp/#/*/type
               values
      add:x:@.return/*/*/values
         get-nodes:x:@invoke/*
      add:x:../*/return
         get-nodes:x:@.return/*

// Returning result of above invocation to caller.
return
