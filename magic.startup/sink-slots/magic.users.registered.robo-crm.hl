
/*
 * Invoked when a new user is registering in the system.
 */
slots.create:magic.users.registered.robo-crm

   // Sanity checking invocation.
   validators.mandatory:x:@.arguments/*/username

   // Inserting user into contacts table.
   data.connect:robo-crm
   
      // Normalising subscribe.
      set-value:x:@.arguments/*/subscribe/=yes
         .:int:1
      set-value:x:@.arguments/*/subscribe/=no
         .:int:0
      set-name:x:@.arguments/*/subscribe
         .:subscriber
   
      // Adding name and email if specified.
      add:x:./*/data.create/*/values
         get-nodes:x:@.arguments/*/name
         get-nodes:x:@.arguments/*/email
         get-nodes:x:@.arguments/*/subscriber
      data.create
         table:contacts
         values
            meta:x:@.arguments/*/username

      // Logging and informing users.
      log.info:Robo CRM - Contact registered in HUB
      sockets.signal:robo-crm.system
         roles:root, admin, account_manager
         args
            message:Contact registered in HUB
