
/*
 * Creates a task that runs periodically, to check the next upcoming activities,
 * and notify user both on email and in UI that task is due.
 */

// Ensuring we delete previously created task with the same name.
tasks.list:robo-crm.activities.ticker
if
   mt
      get-count:x:@tasks.list/*
      .:int:0
   .lambda
      tasks.delete:robo-crm.activities.ticker

// Creating new task.
tasks.create:robo-crm.activities.ticker
   description:Task that informs users about their upcoming activities 20 minutes or less from now
   repeats:5.minutes
   .lambda

      // Finding date and time offset for which activities to select.
      .max
      set-value:x:@.max
         math.add
            date.now
            time
               minutes:20

      // Finding tasks that are due in the next 20 minutes and that we haven't notified user about.
      data.connect:robo-crm
         data.read
            table:activities
            columns
               type
               due
               username
               description
               contact_id
               activity_id
            where
               and
                  closed.eq
                  due.lt:x:@.max
                  due.neq
                  notified.eq:int:0
            limit:-1
            order:due

         // Looping through each activity resulting from above invocation.
         for-each:x:@data.read/*

            // Creating info message that we transmit to user.
            .data
               type:x:@.dp/#/*/type
               due:x:@.dp/#/*/due
               username:x:@.dp/#/*/username
               description:x:@.dp/#/*/description
               activity_id:x:@.dp/#/*/activity_id
               contact_name
            unwrap:x:-/*

            /*
             * Updating activity, setting notified to 1 to avoid duplicate notifications
             * for the same activity.
             */
            data.update
               table:activities
               values
                  notified:int:1
               where
                  and
                     activity_id.eq:x:@.data/*/activity_id

            // Retrieving contact name if activity is associated with a contact.
            if
               not-null:x:@.dp/#/*/contact_id
               .lambda
                  data.read
                     table:contacts
                     columns
                        name
                     where
                        and
                           contact_id.eq:x:@.dp/#/*/contact_id
                  set-value:x:@.data/*/contact_name
                     get-value:x:@data.read/*/*/name

            // Dynamically creating message for user.
            .message:Upcoming activity
            if
               not-null:x:@.data/*/contact_name
               .lambda
                  set-value:x:@.message
                     strings.concat
                        get-value:x:@.message
                        .:" with "
                        get-value:x:@.data/*/contact_name

            // Adding time to message.
            set-value:x:@.message
               strings.concat
                  get-value:x:@.message
                  .:" in less than 20 minutes from now"

            // Signaling user over socket connection.
            unwrap:x:+/*/args/**
            sockets.signal:robo-crm.system
               users:x:@.dp/#/*/username
               args
                  message:x:@.message

            // Verifying cloudlet has configured SMTP settings.
            config.get:"magic:smtp:host"
            if
               not-null:x:@config.get
               .lambda

                  // Sending user an email about activity if user has an email address configured.
                  data.connect:[generic|magic]
                     data.read
                        table:users_extra
                        columns
                           type
                           value
                        where
                           and
                              user.eq:x:@.data/*/username
                              or
                                 type.eq:email
                                 type.eq:name

                     // Verifying that user has an email address.
                     if
                        not-null:x:@data.read/*/*/type/=email/./*/value
                        .lambda

                           // Adding description of activity to email being sent.
                           set-value:x:@.message
                              strings.concat
                                 get-value:x:@.message
                                 .:"\r\n"
                                 get-value:x:@.data/*/description

                           // Sending user email about activity.
                           unwrap:x:+/**
                           mail.smtp.send
                              message
                                 to
                                    .
                                       name:x:@data.read/*/*/type/=name/./*/value
                                       email:x:@data.read/*/*/type/=email/./*/value
                                 subject:Upcoming activity
                                 entity:text/plain
                                    content:x:@.message

                           // Logging that we sent an email to contact for activity.
                           strings.concat
                              .:"Aista CRM - Email sent to "
                              get-value:x:@.data/*/username
                           log.info:x:-

                     else

                        // Logging that we could NOT send an email to contact for activity.
                        log.info:Aista CRM - Email NOT sent to user
                           username:x:@.data/*/username
                           reason:No email address associated with user

            else

               // Logging that we could NOT send an email to contact for activity.
               log.info:Aista CRM - Email NOT sent to user
                  username:x:@.data/*/username
                  reason:No SMTP settings configured for cloudlet
